\section{Potentials and energies}

Total energy per unit cell system
$E^{\mathrm{KS}}_{\mathrm{total}}$ can be written as
\begin{equation}
E^{\mathrm{KS}}_{\mathrm{total}} =
E_{\mathrm{kin}} + E_{\mathrm{ele-nuc}} +
E_{\mathrm{Ha}} + E_{\mathrm{xc}} + E_{\mathrm{nuc-nuc}}
\label{eq:E_KS_total}
\end{equation}

Kohn-Sham equations:
\begin{equation}
H_{\mathrm{KS}} \psi_{i\mathbf{k}}(\mathbf{r}) =
\epsilon_{i\mathbf{k}} \psi_{i\mathbf{k}}(\mathbf{r})
\end{equation}

\input{images/Potentials_struct}

\input{images/Energies_struct}

\subsection{Electron density}

Electron density $\rho(\mathbf{r})$ is calculated as:
\begin{equation}
\rho(\mathbf{r}) = \sum_{i=1}^{N_{\mathrm{occ}}} f_{i} \psi^{*}_{i}(\mathbf{r})
\psi_{i}(\mathbf{r})
\end{equation}

Function: \jlinline{calc_rhoe!} and \jlinline{calc_rhoe}.

\subsection{Kinetic energy}

Kinetic energy:
\begin{equation}
E_{\mathrm{kin}} = -\frac{1}{2} \sum_{\mathbf{k}} \sum_{i}
w_{\mathbf{k}} f_{i\mathbf{k}}
\int_{\Omega}
\psi^{*}_{i\mathbf{k}}(\mathbf{r})
\nabla^2
\psi_{i\mathbf{k}}(\mathbf{r})
\,
\mathrm{d}\mathbf{r}
\label{eq:Kin_energy}
\end{equation}

In reciprocal space:
\begin{equation}
E_{\mathrm{kin}} =
\frac{1}{2} \sum_{\mathbf{k}} \sum_{i=1}^{N_{\mathrm{occ}}}
w_{\mathbf{k}} f_{i\mathbf{k}}
\sum_{\mathbf{G}} \left| \mathbf{G} + \mathbf{k} \right|^2
\left|c_{i,\mathbf{G}+\mathbf{k}}\right|^2
\end{equation}


\subsection{Local and nonlocal pseudopotential energy}

The local pseudopotential contribution is
\begin{equation}
E^{\mathrm{PS}}_{\mathrm{loc}} =
\int_{\Omega} \rho(\mathbf{r})\,V^{\mathrm{PS}}_{\mathrm{loc}}(\mathbf{r})\,
\mathrm{d}\mathbf{r}
\end{equation}
%
and the non-local contribution is
\begin{equation}
E^{\mathrm{PS}}_{\mathrm{nloc}} = 
\sum_{\mathbf{k}}
\sum_{i}
w_{\mathbf{k}}
f_{i\mathbf{k}}
\int_{\Omega}\,
\psi^{*}_{i\mathbf{k}}(\mathbf{r})
\left[
\sum_{I}\sum_{l=0}^{l_{\mathrm{max}}}
V^{\mathrm{PS}}_{l}(\mathbf{r}-\mathbf{R}_{I},\mathbf{r}'-\mathbf{R}_{I})
\right]
\psi_{i\mathbf{k}}(\mathbf{r})
\,\mathrm{d}\mathbf{r}.
\end{equation}

\subsection{Hartree energy}

\begin{equation}
E_{\mathrm{Ha}} = \frac{1}{2}
\int_{\Omega}
V_{\mathrm{Ha}}(\mathbf{r})\rho(\mathbf{r})\,
\mathrm{d}\mathbf{r}
\end{equation}
where the Hartree potential $V_{\mathrm{Ha}}$ is defined as
\begin{equation}
V_{\mathrm{Ha}}(\mathbf{r}) =
\int_{\Omega}
\frac{\rho(\mathbf{r}')}{\mathbf{r} - \mathbf{r}'}
\,\mathrm{d}\mathbf{r}'
\end{equation}
Alternatively, $V_{\mathrm{Ha}}$ can be calculated as the solution of Poisson
equation
\begin{equation}
\nabla^2 V_{\mathrm{Ha}}(\mathbf{r}) =
-4\pi\rho(\mathbf{r})
\end{equation}

\subsection{XC energy and potential}

\textsf{PWDFT.jl} uses \textsf{Libxc.jl}\cite{Libxc.jl}, a Julia wrapper to
\textsf{Libxc}\cite{Marques2012,Lehtola2018}, to calculate exchange correlation
energy and potentials.

For LDA we have:
\begin{align}
E_{\mathrm{xc}}\left[\rho_{\sigma}\right] & = \int \epsilon^{\mathrm{HEG}}_{\mathrm{xc}}
\left[ \rho_{\sigma}(\mathbf{r}) \right]
\rho_{\text{tot}}(\mathbf{r})\, \mathrm{d}\mathbf{r} \\
& = \int \left\{
\epsilon^{\mathrm{HEG}}_{\mathrm{x}} \left[ \rho_{\sigma}(\mathbf{r}) \right] +
\epsilon^{\mathrm{HEG}}_{\mathrm{c}} \left[ \rho_{\sigma}(\mathbf{r}) \right]
\right\}
\rho(\mathbf{r})\, \mathrm{d}\mathbf{r}
\end{align}

\begin{equation}
\delta E_{\mathrm{xc}}\left[\rho_{\sigma}\right] =
\sum_{\sigma} \int
\left(
\epsilon^{\mathrm{HEG}}_{\mathrm{xc}} +
\rho_{\mathrm{tot}} \frac{\partial}{\partial \rho_{\sigma}} \epsilon^{\mathrm{HEG}}_{\mathrm{xc}}
\right)
\, \mathrm{d}\mathbf{r}\,\delta \rho_{\sigma}
\end{equation}


\subsubsection{Calculation of $E_{\mathrm{xc}}$ in \textsf{PWDFT.jl} using Libxc}

Note:

For VWN functional (should be applicable to other LDA functionals), we have the following
for non-spin-polarized case:
%
\begin{juliacode}
function calc_epsxc_VWN( Rhoe::Array{Float64,1} )
  Npoints = size(Rhoe)[1]
  Nspin = 1
  eps_x = zeros(Float64,Npoints)
  eps_c = zeros(Float64,Npoints)

  ptr = Libxc.xc_func_alloc()
  
  # exchange part
  Libxc.xc_func_init(ptr, Libxc.LDA_X, Nspin)
  Libxc.xc_lda_exc!(ptr, Npoints, Rhoe, eps_x)
  Libxc.xc_func_end(ptr)

  # correlation part
  Libxc.xc_func_init(ptr, Libxc.LDA_C_VWN, Nspin)
  Libxc.xc_lda_exc!(ptr, Npoints, Rhoe, eps_c)
  Libxc.xc_func_end(ptr)

  Libxc.xc_func_free(ptr)

  return eps_x + eps_c
end
\end{juliacode}


\begin{juliacode}
function calc_epsxc_VWN( Rhoe::Array{Float64,2} )
  Nspin = size(Rhoe)[2]
  Npoints = size(Rhoe)[1]
  if Nspin == 1
    return calc_epsxc_VWN( Rhoe[:,1] )
  end

  # Do the transpose manually
  Rhoe_tmp = zeros(2*Npoints)
  ipp = 0
  for ip = 1:2:2*Npoints
    ipp = ipp + 1
    Rhoe_tmp[ip] = Rhoe[ipp,1]
    Rhoe_tmp[ip+1] = Rhoe[ipp,2]
  end

  # ....
  # The rest of the code are similar to non-spin polarized case,
  # however now we use `Nspin=2` and pass `Rhoe_tmp` instead of `Rhoe`
end
\end{juliacode}

For PBE or any gradient-corrected functionals:
\begin{juliacode}
function calc_epsxc_PBE( pw::PWGrid, Rhoe::Array{Float64,1} )
  Npoints = size(Rhoe)[1]
  Nspin = 1

  # calculate gRhoe2
  gRhoe = op_nabla( pw, Rhoe )
  gRhoe2 = zeros( Float64, Npoints )
  for ip = 1:Npoints
    gRhoe2[ip] = dot( gRhoe[:,ip], gRhoe[:,ip] )
  end

  eps_x = zeros(Float64,Npoints)
  eps_c = zeros(Float64,Npoints)

  ptr = Libxc.xc_func_alloc()

  # exchange part
  Libxc.xc_func_init(ptr, Libxc.GGA_X_PBE, Nspin)
  Libxc.xc_gga_exc!(ptr, Npoints, Rhoe, gRhoe2, eps_x)
  Libxc.xc_func_end(ptr)

  # correlation part
  Libxc.xc_func_init(ptr, Libxc.GGA_C_PBE, Nspin)
  Libxc.xc_gga_exc!(ptr, Npoints, Rhoe, gRhoe2, eps_c)
  Libxc.xc_func_end(ptr)

  Libxc.xc_func_free(ptr)

  return eps_x + eps_c
end
\end{juliacode}

For PBE spin-polarized case

\begin{juliacode}
function calc_epsxc_PBE( pw::PWGrid, Rhoe::Array{Float64,2} )
  Nspin = size(Rhoe)[2]
  if Nspin == 1
    return calc_epsxc_PBE( pw, Rhoe[:,1] )
  end

  Npoints = size(Rhoe)[1]

  # calculate gRhoe2
  gRhoe_up = op_nabla( pw, Rhoe[:,1] )
  gRhoe_dn = op_nabla( pw, Rhoe[:,2] )
  gRhoe2 = zeros( Float64, 3*Npoints )
  ipp = 0
  for ip = 1:3:3*Npoints
    ipp = ipp + 1
    gRhoe2[ip]   = dot( gRhoe_up[:,ipp], gRhoe_up[:,ipp] )
    gRhoe2[ip+1] = dot( gRhoe_up[:,ipp], gRhoe_dn[:,ipp] )
    gRhoe2[ip+2] = dot( gRhoe_dn[:,ipp], gRhoe_dn[:,ipp] )
  end

  Rhoe_tmp = zeros(2*Npoints)
  ipp = 0
  for ip = 1:2:2*Npoints
    ipp = ipp + 1
    Rhoe_tmp[ip] = Rhoe[ipp,1]
    Rhoe_tmp[ip+1] = Rhoe[ipp,2]
  end

  # ....
  # The rest of the code are similar to non-spin polarized case,
  # however now we use `Nspin=2` and pass `Rhoe_tmp` instead of `Rhoe`
end
\end{juliacode}


\subsubsection{Calculation of $V_{\mathrm{xc}}$ in \textsf{PWDFT.jl} using Libxc}

VWN non-spin polarized:
\begin{juliacode}
function calc_Vxc_VWN( Rhoe::Array{Float64,1} )
  Npoints = size(Rhoe)[1]
  Nspin = 1
  v_x = zeros(Float64,Npoints)
  v_c = zeros(Float64,Npoints)

  ptr = Libxc.xc_func_alloc()

  # exchange part
  Libxc.xc_func_init(ptr, Libxc.LDA_X, Nspin)
  Libxc.xc_lda_vxc!(ptr, Npoints, Rhoe, v_x)
  Libxc.xc_func_end(ptr)

  # correlation part
  Libxc.xc_func_init(ptr, Libxc.LDA_C_VWN, Nspin)
  Libxc.xc_lda_vxc!(ptr, Npoints, Rhoe, v_c)
  Libxc.xc_func_end(ptr)

  Libxc.xc_func_free(ptr)
  
  return v_x + v_c
end
\end{juliacode}

VWN spin-polarized:
\begin{juliacode}
function calc_Vxc_VWN( Rhoe::Array{Float64,2} )
  Nspin = size(Rhoe)[2]
  if Nspin == 1
    return calc_Vxc_VWN( Rhoe[:,1] )
  end

  Npoints = size(Rhoe)[1]

  Vxc = zeros( Float64, Npoints, 2 )
  V_x = zeros( Float64, 2*Npoints )
  V_c = zeros( Float64, 2*Npoints )

  # This is the transposed version of Rhoe, use copy
  Rhoe_tmp = zeros(2*Npoints)
  ipp = 0
  for ip = 1:2:2*Npoints
    ipp = ipp + 1
    Rhoe_tmp[ip] = Rhoe[ipp,1]
    Rhoe_tmp[ip+1] = Rhoe[ipp,2]
  end

  ptr = Libxc.xc_func_alloc()

  # exchange part
  Libxc.xc_func_init(ptr, Libxc.LDA_X, Nspin)
  Libxc.xc_lda_vxc!(ptr, Npoints, Rhoe_tmp, V_x)
  Libxc.xc_func_end(ptr)

  # correlation part
  Libxc.xc_func_init(ptr, Libxc.LDA_C_VWN, Nspin)
  Libxc.xc_lda_vxc!(ptr, Npoints, Rhoe_tmp, V_c)
  Libxc.xc_func_end(ptr)

  Libxc.xc_func_free(ptr)

  ipp = 0
  for ip = 1:2:2*Npoints
    ipp = ipp + 1
    Vxc[ipp,1] = V_x[ip] + V_c[ip]
    Vxc[ipp,2] = V_x[ip+1] + V_c[ip+1]
  end
  return Vxc
end
\end{juliacode}

PBE non-spin-polarized:
\begin{juliacode}
function calc_Vxc_PBE( pw::PWGrid, Rhoe::Array{Float64,1} )
  Npoints = size(Rhoe)[1]
  Nspin = 1

  # calculate gRhoe2
  gRhoe = op_nabla( pw, Rhoe )
  gRhoe2 = zeros( Float64, Npoints )
  for ip = 1:Npoints
    gRhoe2[ip] = dot( gRhoe[:,ip], gRhoe[:,ip] )
  end

  V_x = zeros(Float64,Npoints)
  V_c = zeros(Float64,Npoints)
  V_xc = zeros(Float64,Npoints)

  Vg_x = zeros(Float64,Npoints)
  Vg_c = zeros(Float64,Npoints)
  Vg_xc = zeros(Float64,Npoints)

  ptr = Libxc.xc_func_alloc()

  # exchange part
  Libxc.xc_func_init(ptr, Libxc.GGA_X_PBE, Nspin)
  Libxc.xc_gga_vxc!(ptr, Npoints, Rhoe, gRhoe2, V_x, Vg_x)
  Libxc.xc_func_end(ptr)

  # correlation part
  Libxc.xc_func_init(ptr, Libxc.GGA_C_PBE, Nspin)
  Libxc.xc_gga_vxc!(ptr, Npoints, Rhoe, gRhoe2, V_c, Vg_c)
  Libxc.xc_func_end(ptr)

  V_xc = V_x + V_c
  Vg_xc = Vg_x + Vg_c

  # gradient correction
  h = zeros(Float64,3,Npoints)
  divh = zeros(Float64,Npoints)
  for ip = 1:Npoints
    h[1,ip] = Vg_xc[ip] * gRhoe[1,ip]
    h[2,ip] = Vg_xc[ip] * gRhoe[2,ip]
    h[3,ip] = Vg_xc[ip] * gRhoe[3,ip]
  end
  # div ( vgrho * gRhoe )
  divh = op_nabla_dot( pw, h )
  for ip = 1:Npoints
    V_xc[ip] = V_xc[ip] - 2.0*divh[ip]
  end
  
  return V_xc
end
\end{juliacode}

PBE spin-polarized:
\begin{juliacode}
function calc_Vxc_PBE( pw::PWGrid, Rhoe::Array{Float64,2} )
  Nspin = size(Rhoe)[2]
  if Nspin == 1
    return calc_Vxc_PBE( pw, Rhoe[:,1] )
  end

  Npoints = size(Rhoe)[1]

  # calculate gRhoe2
  gRhoe_up = op_nabla( pw, Rhoe[:,1] ) # gRhoe = ∇⋅Rhoe
  gRhoe_dn = op_nabla( pw, Rhoe[:,2] )
  gRhoe2 = zeros( Float64, 3*Npoints )
  ipp = 0
  for ip = 1:3:3*Npoints
    ipp = ipp + 1
    gRhoe2[ip]   = dot( gRhoe_up[:,ipp], gRhoe_up[:,ipp] )
    gRhoe2[ip+1] = dot( gRhoe_up[:,ipp], gRhoe_dn[:,ipp] )
    gRhoe2[ip+2] = dot( gRhoe_dn[:,ipp], gRhoe_dn[:,ipp] )
  end

  V_xc = zeros(Float64, Npoints, 2)
  V_x  = zeros(Float64, Npoints*2)
  V_c  = zeros(Float64, Npoints*2)

  Vg_xc = zeros(Float64, 3, Npoints)
  Vg_x  = zeros(Float64, 3*Npoints)
  Vg_c  = zeros(Float64, 3*Npoints)

  Rhoe_tmp = zeros(2*Npoints)
  ipp = 0
  for ip = 1:2:2*Npoints
    ipp = ipp + 1
    Rhoe_tmp[ip] = Rhoe[ipp,1]
    Rhoe_tmp[ip+1] = Rhoe[ipp,2]
  end

  ptr = Libxc.xc_func_alloc()

  # exchange part
  Libxc.xc_func_init(ptr, Libxc.GGA_X_PBE, Nspin)
  Libxc.xc_gga_vxc!(ptr, Npoints, Rhoe_tmp, gRhoe2, V_x, Vg_x)
  Libxc.xc_func_end(ptr)

  # correlation part
  Libxc.xc_func_init(ptr, Libxc.GGA_C_PBE, Nspin)
  Libxc.xc_gga_vxc!(ptr, Npoints, Rhoe_tmp, gRhoe2, V_c, Vg_c)
  Libxc.xc_func_end(ptr)

  ipp = 0
  for ip = 1:2:2*Npoints
    ipp = ipp + 1
    V_xc[ipp,1] = V_x[ip] + V_c[ip]
    V_xc[ipp,2] = V_x[ip+1] + V_c[ip+1]
  end

  Vg_xc = reshape(Vg_x + Vg_c, (3,Npoints))

  h = zeros(Float64,3,Npoints)
  divh = zeros(Float64,Npoints)

  # spin up
  for ip = 1:Npoints
    h[1,ip] = 2*Vg_xc[1,ip]*gRhoe_up[1,ip] + Vg_xc[2,ip]*gRhoe_dn[1,ip]
    h[2,ip] = 2*Vg_xc[1,ip]*gRhoe_up[2,ip] + Vg_xc[2,ip]*gRhoe_dn[2,ip]
    h[3,ip] = 2*Vg_xc[1,ip]*gRhoe_up[3,ip] + Vg_xc[2,ip]*gRhoe_dn[3,ip]
  end
  divh = op_nabla_dot( pw, h )
  for ip = 1:Npoints
    V_xc[ip,1] = V_xc[ip,1] - divh[ip]
  end

  # Spin down
  for ip = 1:Npoints
    h[1,ip] = 2*Vg_xc[3,ip]*gRhoe_dn[1,ip] + Vg_xc[2,ip]*gRhoe_up[1,ip]
    h[2,ip] = 2*Vg_xc[3,ip]*gRhoe_dn[2,ip] + Vg_xc[2,ip]*gRhoe_up[2,ip]
    h[3,ip] = 2*Vg_xc[3,ip]*gRhoe_dn[3,ip] + Vg_xc[2,ip]*gRhoe_up[3,ip]
  end
  divh = op_nabla_dot( pw, h )
  for ip = 1:Npoints
    V_xc[ip,2] = V_xc[ip,2] - divh[ip]
  end
  
  return V_xc
end
\end{juliacode}
